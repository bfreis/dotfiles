########################
# Begin DOTFILES zshrc #
########################

export DOTFILES_HOME=${HOME}/dev/dotfiles

source "${DOTFILES_HOME}"/lib/df.zsh

# Initialize homebrew
__df::init_homebrew

# Load any local, non-public configuration
# ~/.localrc is a good place to configure API keys, tokens, etc
__df::source_if_exists ~/.localrc

# load the list of all modules
typeset -U config_files
config_files=("${DOTFILES_HOME}"/modules/**/*.zsh)

# first load scripts configuring the PATH env var
typeset -U path
for file in ${(M)config_files:#*/path.zsh}; do
  __df::source_if_exists "${file}"
done
export PATH

# then load everything but not the completion.zsh just yet
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  __df::source_if_exists "${file}"
done

# now initialize the completion system
autoload -Uz compinit
compinit

# and load the custom completion files
for file in ${(M)config_files:#*/completion.zsh}; do
  __df::source_if_exists "${file}"
done

# Load any local, non-public configuration
# ~/.localrc.end is a good place to configure overrides
__df::source_if_exists ~/.localrc.end

## clean up, and done
unset config_files
__df::cleanup_funcs

######################
# End DOTFILES zshrc #
######################
